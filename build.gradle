apply plugin: 'java'
apply plugin: 'distribution'

group='com.percussion'
version='7.0.0'

repositories {

    flatDir {

        def rhythmyxHome = System.getenv('RHYTHMYX_HOME')

        dirs "${rhythmyxHome}/jetty/base/webapps/Rhythmyx/WEB-INF/lib"
        dirs "${rhythmyxHome}/jetty/base/webapps/RxServices/WEB-INF/lib"
        dirs "${rhythmyxHome}/jetty/defaults/lib/perc"
        dirs "${rhythmyxHome}/jetty/defaults/lib/perc-logging"
        dirs "lib"
    }

    jcenter()
}

dependencies {

    compile 'org.slf4j:slf4j-api:1.7.22'

    testCompile 'junit:junit:4.12'

    compile('org.apache.xmlgraphics:fop:2.2') {
        exclude group: 'org.apache.avalon.framework'
    }

    compile 'avalon-framework:avalon-framework:4.1.5'

    compile name: 'commons-lang-2.4'
    compile name: 'commons-jexl-1.1.1-patched'
    compile name: 'rxclient'
    compile name: 'rxservices'
    compile name: 'rxutils'
    compile name: 'rxbusiness'
}

def findJar(String prefix) {
    configurations.compile.filter {
        it.name.startsWith(prefix)
    }
}

distributions {
    main {
        contents {
            into('fop_and_avalon_libs') {
                fileMode = 0750
                from('lib') {
                    include "commons-jexl-1.1.1-patched.jar"
                }
                from findJar('fop')
                from findJar('avalon-framework')
                from findJar('xmlgraphics')
                from findJar('batik')
            }
            from('rxconfig') {
                fileMode = 0750
                into 'rxconfig'
            }
            from('Samples') {
                fileMode = 0750
                into 'Samples'
            }
            from('img') {
                fileMode = 0750
                into 'img'
            }
            into('.') {
                fileMode = 0750
                from "build/libs/${rootProject.name}-${version}.jar"
                from 'README.md'
                from 'README.html'
                from 'install.gradle'
                from 'Extensions.xml'
                from 'gradlew'
                from 'gradlew.bat'
                rename 'install.gradle', 'build.gradle'
            }
            from('gradle') {
                fileMode = 0750
                into 'gradle'
            }
        }
    }
}
